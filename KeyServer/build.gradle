plugins {
    id 'java'
}

group = 'nsu.kochanov'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.register('runGenCa', JavaExec) {
    group = 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'nsu.kochanov.tools.GenCaKey'
    if (project.hasProperty('out')) args(project.property('out'))
}

tasks.register('runServer', JavaExec) {
    group = 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'nsu.kochanov.server.ServerApp'
    if (project.hasProperty('port')) args('--port', project.property('port'))
    if (project.hasProperty('threads')) args('--threads', project.property('threads'))
    if (project.hasProperty('issuer')) args('--issuer', project.property('issuer'))
    if (project.hasProperty('cakey')) args('--ca-key', project.property('cakey'))
}

tasks.register('runClient', JavaExec) {
    group = 'application'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'nsu.kochanov.client.KeyClient'
    if (project.hasProperty('host')) args('--host', project.property('host'))
    if (project.hasProperty('port')) args('--port', project.property('port'))
    // ВАЖНО: нельзя использовать свойство 'name' (занято Gradle: project.name)
    if (project.hasProperty('user')) args('--name', project.property('user'))
    if (project.hasProperty('delay')) args('--delay', project.property('delay'))
    if (project.hasProperty('crash')) args('--crash', project.property('crash'))
    if (project.hasProperty('out')) args('--out', project.property('out'))
}